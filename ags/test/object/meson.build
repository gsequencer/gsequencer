# Copyright (C) 2020-2024 Daniel Maksymow, Joël Krähemann
# 
# Copying and distribution of this file, with or without modification,
# are permitted in any medium without royalty provided the copyright
# notice and this notice are preserved.  This file is offered as-is,
# without any warranty.

static_tests = [
  'ags_application_context_test',
  'ags_config_test',
  'ags_countable_test',
  'ags_main_loop_test',
  'ags_mutable_test',
  'ags_connectable_test',
  'ags_seekable_test',
  'ags_soundcard_test',
  'ags_tactable_test',
]

static_test_dependencies = [
  common_test_dependencies,
  xml2_dependency,
  soup_dependency,
  instpatch_dependency,
  dl_dependency,
]

foreach static_test : static_tests
  source = ['@0@.c'.format(static_test)]
  if static_test.contains('/')
    tmp = static_test.split('/')
    static_test = '_'.join(tmp)
  endif

  static_test_bin = executable(
    static_test,
    source,
    c_args: [compiler_test_flags],
    include_directories: [includes],
    dependencies: [static_test_dependencies],
    link_with: [
      libags,
      libags_thread,
      libags_server,
      libags_audio,
    ]
  )

  test(static_test, static_test_bin)
endforeach

